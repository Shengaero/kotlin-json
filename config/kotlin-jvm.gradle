/*
 * Copyright 2018 Kaidan Gustave
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
project.ext.kotlinJvmConfig = { boolean common = false, int jdkVersion = 8 ->
    apply plugin: !common ? 'kotlin' : 'kotlin-platform-jvm'

    // Test with junit5 when jdk is 8+
    if(jdkVersion == 8) {
        apply plugin: 'org.junit.platform.gradle.plugin'
    }

    sourceCompatibility = jdkVersion

    sourceSets {
        test.kotlin.srcDirs += 'src/test/kotlin'
        test.compileClasspath += main.compileClasspath
        test.runtimeClasspath += main.runtimeClasspath + main.compileClasspath
    }

    dependencies {
        compileOnly kotlin('stdlib')
        if(jdkVersion == 8) {
            compileOnly kotlin('stdlib-jdk8')
            compileOnly kotlin('stdlib-jdk7')

            testCompile junit('api')
            testCompile junit('params')
            testRuntime junit('engine')
        } else {
            testCompile 'junit:junit:4.12'
        }

        testCompile kotlin('test')
        testCompile kotlin('test-junit')
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.$jdkVersion"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.$jdkVersion"
        }
    }
}